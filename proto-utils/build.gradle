buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath libs.protobuf_gradle
    }
}

apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

protobuf {
    generatedFilesBaseDir = "$projectDir/src/main/generated"
    protoc {
        artifact = libs.protoc
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

dependencies {
    implementation libs.protobuf
    implementation libs.protobuf_util

    testImplementation libs.test
    testImplementation libs.protobuf
    testImplementation libs.protobuf_util
}

jar {
    def cmd = "git rev-parse --short HEAD"
    def proc = cmd.execute()
    ext.revision = proc.text.trim()

    manifest {
        attributes(
                "Implementation-Version": version + " build: " + ext.revision,
                "releases-date": new Date().format("yyyy-MM-dd'T 'HH:mm:ss.SSSZZ"),
                "Specification-Vendor": "Volmyr.com",
                "Specification-Title": "Proto utils"
        )
    }

    baseName 'ptoto-utils'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.volmyr'
            artifactId = 'proto-utils'
            version = version
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Proto Utils library'
                description = 'Provide util methods for working with Google proto'
                developers {
                    developer {
                        id = 'volodymyrd'
                        name = 'Volodymyr Dotsenko'
                        email = 'volodymyrdotsenko@volmyr.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "GithubPublicMavenRepo"
            url = uri("https://maven.pkg.github.com/volodymyrd/public-mvn-repo")
            credentials {
                // ~/.gradle/gradle.properties
                username = project.findProperty("repo.user") ?: System.getenv("USERNAME")
                password = project.findProperty("public_mvn_repo.key") ?: System.getenv("TOKEN")
            }
        }
    }
}
