defaultTasks 'clean', 'build'

subprojects {
    ext {
        versions = [
                assertj        : '3.16.1',
                auto_value     : '1.7.4',
                google_auth    : '0.21.1',
                guava          : '29.0-jre',
                junit          : '4.13',
                javapoet       : '1.13.0',
                protobuf_gradle: '0.8.12',
                protobuf       : '3.12.2',
                servlet_api    : '2.5',
        ]

        libs = [:]

        libs.auto_value = ["com.google.auto.value:auto-value-annotations:${versions.auto_value}",
                           "com.google.auto.value:auto-value:${versions.auto_value}"]

        libs.google_auth = [
                "com.google.auth:google-auth-library-oauth2-http:${versions.google_auth}"]

        libs.guava = ["com.google.guava:guava:${versions.guava}"]

        libs.javapoet = "com.squareup:javapoet:${versions.javapoet}"

        libs.protobuf = ["com.google.protobuf:protobuf-java:${versions.protobuf}"]

        libs.protobuf_util = ["com.google.protobuf:protobuf-java-util:${versions.protobuf}"]

        libs.protobuf_gradle =
                "com.google.protobuf:protobuf-gradle-plugin:${versions.protobuf_gradle}"

        libs.protoc = "com.google.protobuf:protoc:${versions.protobuf}"

        libs.servlet_api = "javax.servlet:servlet-api:${versions.servlet_api}"

        libs.test = ["junit:junit:${versions.junit}", "org.assertj:assertj-core:${versions.assertj}"]
    }

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = targetCompatibility = 1.8

    compileJava {
        options.encoding = "UTF-8"
        options.annotationProcessorPath = configurations.annotationProcessor
        options.setAnnotationProcessorGeneratedSourcesDirectory(file("$projectDir/src/main/generated"))
    }
    compileTestJava {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '6.3'
    }

    buildDir = 'target'

    version = '0.0.25-SNAPSHOT'

    test {
        testLogging {
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    println "Results: " +
                            "${result.resultType} (${result.testCount} tests, " +
                            "${result.successfulTestCount} successes, ${result.failedTestCount} failures, " +
                            "${result.skippedTestCount} skipped)"
                }
            }
        }
    }

    publishing {
        repositories {
            maven {
                name = "GithubPublicMavenRepo"
                url = uri("https://maven.pkg.github.com/volodymyrd/public-mvn-repo")
                credentials {
                    // ~/.gradle/gradle.properties
                    username = project.findProperty("repo.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("mvn_repo_rw.key") ?: System.getenv("TOKEN")
                }
            }
        }
    }
}
