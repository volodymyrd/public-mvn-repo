dependencies {
    implementation libs.guava
    implementation libs.auto_value
    annotationProcessor libs.auto_value
}

jar {
    def cmd = "git rev-parse --short HEAD"
    def proc = cmd.execute()
    ext.revision = proc.text.trim()

    manifest {
        attributes(
                "Implementation-Version": version + " build: " + ext.revision,
                "releases-date": new Date().format("yyyy-MM-dd'T 'HH:mm:ss.SSSZZ"),
                "Specification-Vendor": "com.volmyr",
                "Specification-Title": "Telephony Api"
        )
    }

    baseName 'telephony-api'
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.volmyr'
            artifactId = 'telephony-api'
            version = version
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Telephony Api'
                description = 'Provide Telephony Api Interface'
                developers {
                    developer {
                        id = 'volodymyrd'
                        name = 'Volodymyr Dotsenko'
                        email = 'volodymyrdotsenko@volmyr.com'
                    }
                }
            }
        }
    }
}
